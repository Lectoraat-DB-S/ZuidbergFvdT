FUNCTION_BLOCK "Lagerpers_EM"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      Start : Bool;
      Stop : Bool;
   END_VAR

   VAR_OUTPUT 
      Voltooid : Bool;
      Gestart : Bool;
   END_VAR

   VAR 
      Grijper { S7_SetPoint := 'False'} : "CilinderB52_CM";
      Pers : "CilinderB52_CM";
      Geleider : "CilinderM52_CM";
      Sluis : "CilinderB52_CM";
      EStop : "E-Stop_CM";
      Stap { S7_SetPoint := 'True'} : Int;
      Prev_Stap : Int;
   END_VAR


BEGIN
	CASE #Stap OF
	    0:
	        #Gestart := FALSE;
	        #Voltooid := TRUE;
	        #Geleider.CMD_Uit := FALSE;
	        #Geleider.CMD_In := TRUE;
	        #Geleider.CMD_Uit := FALSE;
	        #Geleider.CMD_In := TRUE;
	        #Grijper.CMD_Uit := TRUE; //grijper open
	        #Grijper.CMD_In := FALSE; //grijper dicht
	        IF #Start THEN
	            #Stap := #Stap + 1;
	        END_IF;
	    1:
	        #Sluis.CMD_Uit := TRUE;
	        #Sluis.CMD_In := FALSE;
	        #Voltooid := FALSE;
	        #Gestart := TRUE;
	        IF #Sluis.Cil_Uit THEN
	            #Stap := #Stap + 1;
	        END_IF;
	        
	    2:
	        #Sluis.CMD_Uit := FALSE;
	        #Sluis.CMD_In := TRUE;
	        
	        IF #Sluis.Cil_In THEN
	            #Stap := #Stap + 1;
	        END_IF;
	    3:
	        #Voltooid := FALSE;
	        #Grijper.CMD_Uit := FALSE;
	        #Grijper.CMD_In := TRUE;
	        IF #Grijper.Cil_In THEN
	            #Stap := #Stap + 1;
	        END_IF;
	    4:
	        #Geleider.CMD_Uit := TRUE;
	        #Geleider.CMD_In := FALSE;
	        IF #Geleider.Cil_Uit THEN
	            #Stap := #Stap + 1;
	        END_IF;
	        
	    5:
	        #Grijper.CMD_Uit := TRUE;
	        #Grijper.CMD_In := FALSE;
	        IF #Grijper.Cil_Uit THEN
	            #Stap := #Stap + 1;
	        END_IF;
	        
	    6:
	        #Pers.CMD_Uit := TRUE;
	        #Pers.CMD_In := FALSE;
	        IF #Pers.Cil_Uit THEN
	            #Stap := #Stap + 1;
	        END_IF;
	    7:
	        #Pers.CMD_Uit := FALSE;
	        #Pers.CMD_In := TRUE;
	        IF #Pers.Cil_In THEN
	            #Stap := #Stap + 1;
	        END_IF;
	    8:
	            #Geleider.CMD_Uit := FALSE;
	            #Geleider.CMD_In := TRUE;
	            IF #Geleider.Cil_In THEN
	                #Stap := 0;
	            END_IF;
	    20:
	        IF "AssemblageCel_U_DB".Start THEN
	            #Stap := #Prev_Stap;
	        END_IF;
	END_CASE;
	
	IF #Stop OR #EStop.EStop_Active THEN
	    IF #Stap <> 20 THEN
	        #Prev_Stap := #Stap;
	    END_IF;
	    #Stap := 20;
	END_IF;
	
	#Pers();
	#Grijper();
	#Geleider();
	#Sluis();
	#EStop();
END_FUNCTION_BLOCK

